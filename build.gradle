plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'application'
    id 'checkstyle'
    id 'jacoco'
    id 'java'
    id 'org.springframework.boot' version '3.2.4'
    id 'io.spring.dependency-management' version '1.1.4'
}


java {
    sourceCompatibility = '21'
}

group 'nl.medtechchain'

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.34'
    annotationProcessor 'org.projectlombok:lombok:1.18.34'

    implementation 'org.hyperledger.fabric-chaincode-java:fabric-chaincode-shim:2.5.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.17.2'
    implementation 'com.google.protobuf:protobuf-java:3.25.4'
    implementation 'com.google.protobuf:protobuf-java-util:3.25.4'
    implementation 'com.google.privacy.differentialprivacy:differentialprivacy:3.0.0'
    implementation 'com.google.ortools:ortools-java:9.9.3963'
    implementation ':medtechchain.proto:1.0.0'
    implementation 'org.springframework.boot:spring-boot-starter-web'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
    testImplementation 'org.assertj:assertj-core:3.25.3'
    testImplementation 'org.mockito:mockito-core:5.11.0'
}

repositories {
    mavenCentral()
    maven {
        url 'https://jitpack.io'
    }
    flatDir {
        dirs 'libs'
    }
}

//tasks.compileJava {
//    options.release.set(11)
//}

application {
    mainClass = 'org.hyperledger.fabric.contract.ContractRouter'
}

checkstyle {
    toolVersion '10.14.0'
    configFile file("config/checkstyle/checkstyle.xml")
}

configurations.checkstyle {
    resolutionStrategy.capabilitiesResolution.withCapability("com.google.collections:google-collections") {
        select("com.google.guava:guava:0")
    }
}


checkstyleMain {
    source ='src/main/java'
}

checkstyleTest {
    source ='src/test/java'
}

jacocoTestReport {
    dependsOn test
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
        }
    }

    finalizedBy jacocoTestReport
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

shadowJar {
    archiveFileName.set('medtechchain.jar')
    archiveClassifier.set(null)

    manifest {
        attributes 'Main-Class': 'org.hyperledger.fabric.contract.ContractRouter'
    }
}


check.dependsOn jacocoTestCoverageVerification
installDist.dependsOn check

distZip.dependsOn shadowJar
distTar.dependsOn shadowJar

startScripts.dependsOn shadowJar
startShadowScripts.dependsOn jar
